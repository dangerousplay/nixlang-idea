plugins {
    id 'idea'
    id 'org.jetbrains.intellij' version '0.4.21'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id "org.jetbrains.grammarkit" version "2020.1.4"
}

apply plugin: 'org.jetbrains.grammarkit'

import org.jetbrains.grammarkit.tasks.*

group 'org.nixos.lang'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

sourceSets.main.java.srcDirs 'src/main/gen'

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2020.1.1'
}


grammarKit {
    // version of IntelliJ patched JFlex (see bintray link below), Default is 1.7.0-1
    jflexRelease = '1.7.0-1'

    // tag or short commit hash of Grammar-Kit to use (see link below). Default is 2020.1
    grammarKitRelease = '6452fde524'
}

task generateNixLexer(type: GenerateLexer) {
    // source flex file
    source = "grammar/lang/Nix.flex"

    // target directory for lexer
    targetDir = "src/main/gen/org/nixos/idea/lang/"

    // target classname, target file will be targetDir/targetClass.java
    targetClass = "NixLexer"

    // optional, path to the task-specific skeleton file. Default: none
    skeleton = 'grammar/lang/idea-flex.skeleton'

    // if set, plugin will remove a lexer output file before generating new one. Default: false
    purgeOldFiles = true
}

task generateNixParser(type: GenerateParser) {
    // source bnf file
    source = "grammar/lang/Nix.bnf"

    // optional, task-specific root for the generated files. Default: none
    targetRoot = 'src/main/gen'

    // path to a parser file, relative to the targetRoot
    pathToParser = '/org/nixos/idea/lang/NixParser.java'

    // path to a directory with generated psi files, relative to the targetRoot
    pathToPsiRoot = '/org/nixos/idea/lang/psi'

    // if set, plugin will remove a parser output file and psi output directory before generating new ones. Default: false
    purgeOldFiles = true
}


compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}
